
    MODULE SUN_DATA
    DOUBLE PRECISION :: A0(4), A(4), LAMBDA(4), SUMPI(4), C0(4), C(4)
    DOUBLE PRECISION :: Y1_PATCH, Y2_PATCH, Z1_PATCH, Z2_PATCH
    DOUBLE PRECISION :: VEL, DISPX, DISPY, DISPZ
    INTEGER :: NQUAD
    DOUBLE PRECISION :: TIME
    END MODULE SUN_DATA

    PROGRAM SUN6_3
!.....FROM WEXLER
!
!      ********************************************************
!      *                                                      *
!      *                   **** PATCHI ****                   *
!      *                                                      *
!      *   THREE-DIMENSIONAL GROUND-WATER SOLUTE TRANSPORT    *
!      *                                                      *
!      *    MODEL FOR A SEMI-INFINITE AQUIFER OF INFINITE     *
!      *                                                      *
!      *    WIDTH AND HEIGHT. PATCH SOURCE EXTENDING FROM     *
!      *                                                      *
!      *         Y1 TO Y2 AND Z1 TO Z2 LOCATED AT X=0         *
!      *                                                      *
!      *        GROUND-WATER FLOW IN X-DIRECTION ONLY         *
!      *                                                      *
!      *            VERSION CURRENT AS OF 04/01/90            *
!      *                                                      *
!      ********************************************************
!
!       THE FOLLOWING CARD MUST BE CHANGED IF PROBLEM DIMENSIONS ARE
!       GREATER THAN THOSE GIVEN HERE.
!         MAXX = MAXIMUM NUMBER OF X-VALUES
!         MAXY = MAXIMUM NUMBER OF Y-VALUES
!         MAXZ = MAXIMUM NUMBER OF Z-VALUES
!         MAXT = MAXIMUM NUMBER OF TIME VALUES
!         MAXXY = MAXX * MAXY
!         MAXXY2 = 2 * MAXX * MAXY
      PARAMETER (MAXX=100,MAXY=50,MAXZ=30,MAXT=20,MAXXY=5000,   &
      MAXXY2=10000)
      !
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*10 CUNITS,VUNITS,DUNITS,KUNITS,LUNITS,TUNITS
      REAL XP,YP,ZP,CP,TP,DELTA,XPC,YPC,XSCLP,YSCLP
      DIMENSION CXY(MAXX,MAXY),X(MAXX),Y(MAXY),Z(MAXZ),T(MAXT)
      COMMON /PDAT/ XP(MAXX),YP(MAXY),CP(MAXXY),XPC(50),YPC(50), &
         IFLAG(MAXXY2)
      COMMON /IOUNIT/ IN,IO
      CHARACTER*80 TITLE
      COMMON/CSCH/TITLE
!
!     PROGRAM VARIABLES
!
!           NOTE:  ANY CONSISTANT SET OF UNITS MAY BE USED IN THE
!           MODEL.  NO FORMAT STATEMENTS NEED TO BE CHANGED AS
!           LABELS FOR ALL VARIABLES ARE SPECIFIED IN MODEL INPUT.
!
!     C0       SOLUTE CONCENTRATION AT THE INFLOW BOUNDARY [M/L**3]
!     DX       LONGITUDINAL DISPERSION COEFFICIENT [L**2/T]
!     DY       TRANSVERSE (Y-DIRECTION) DISPERSION COEFFICIENT [L**2/T]
!     DZ       TRANSVERSE (Z-DIRECTION) DISPERSION COEFFICIENT [L**2/T]
!     VX       GROUND-WATER VELOCITY IN X-DIRECTION [L/T]
!     DK       FIRST-ORDER SOLUTE DECAY CONSTANT [1/T]
!     X        X-POSITION AT WHICH CONCENTRATION IS EVALUATED [L]
!     Y        Y-POSITION AT WHICH CONCENTRATION IS EVALUATED [L]
!     Z        Z-POSITION AT WHICH CONCENTRATION IS EVALUATED [L]
!     T        TIME AT WHICH CONCENTRATION IS EVALUATED [T]
!     CN       NORMALIZED CONCENTRATION C/C0 [DIMENSIONLESS]
!     CXY      SOLUTE CONCENTRATION C(X,Y,Z,T) [M/L**3]
!     WS       WIDTH OF PATCH SOLUTE SOURCE [L]
!     HS       HEIGHT OF PATCH SOLUTE SOURCE [L]
!     Y1       Y-COORDINATE OF LOWER LIMIT OF PATCH SOLUTE SOURCE [L]
!     Y2       Y-COORDINATE OF UPPER LIMIT OF PATCH SOLUTE SOURCE [L]
!     Z1       Z-COORDINATE OF LOWER LIMIT OF PATCH SOLUTE SOURCE [L]
!     Z2       Z-COORDINATE OF UPPER LIMIT OF PATCH SOLUTE SOURCE [L]
!
!     NX       NUMBER OF X-POSITIONS AT WHICH SOLUTION IS EVALUATED
!     NY       NUMBER OF Y-POSITIONS AT WHICH SOLUTION IS EVALUATED
!     NZ       NUMBER OF Z-POSITIONS AT WHICH SOLUTION IS EVALUATED
!     NT       NUMBER OF TIME VALUES AT WHICH SOLUTION IS EVALUATED
!     NMAX     NUMBER OF TERMS USED IN GAUSS-LEGENDRE NUMERICAL
!              INTEGRATION TECHNIQUE (MUST EQUAL 4, 20, 60, 104 OR 256)
!
!     IPLT     PLOT CONTROL. IF IPLT>0, CONTOUR MAPS ARE PLOTTED
!     XSCLP    SCALING FACTOR TO CONVERT X TO PLOTTER INCHES
!     YSCLP    SCALING FACTOR TO CONVERT Y TO PLOTTER INCHES
!     DELTA    CONTOUR INCREMENT FOR PLOT. (VALUE BETWEEN 0 AND 1.0)
!
!       CHARACTER VARIABLES USED TO SPECIFY UNITS FOR MODEL PARAMETERS
!     CUNITS   UNITS OF CONCENTRATION (M/L**3)
!     VUNITS   UNITS OF GROUND-WATER VELOCITY (L/T)
!     DUNITS   UNITS OF DISPERSION COEFFICIENT (L**2/T)
!     KUNITS   UNITS OF SOLUTE DECAY CONSTANT  (1/T)
!     LUNITS   UNITS OF LENGTH (L)
!     TUNITS   UNITS OF TIME (T)
!
!       DEFINE INPUT/OUTPUT FILES AND PRINT TITLE PAGE

      CALL SETUP()
      CALL CALC_FROM_FILE("..\results\feflow_xy.dat", "..\results\analytical_xy.dat")
      CALL CALC_FROM_FILE("..\results\feflow_xz.dat", "..\results\analytical_xz.dat")
      END
      SUBROUTINE CNRMLP(DK,T,X,Y,Z,Y1,Y2,Z1,Z2,DX,DY,DZ,VX,CN,NMAX)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      COMMON /IOUNIT/ IN,IO
      COMMON /GLPTS/ WN(256),ZN(256)
!
!       THIS ROUTINE CALCULATES THE NORMALIZED CONCENTRATION AT X,Y,Z
!       BASED ON THE ANALYTIC SOLUTION TO THE THREE-DIMENSIONAL
!       ADVECTIVE-DISPERSIVE SOLUTE TRANSPORT EQUATION FOR A SEMI-
!       INFINITE AQUIFER WITH INFINITE WIDTH AND HEIGHT. THE SOLUTE
!       SOURCE HAS A FINITE WIDTH AND HEIGHT, EXTENDING FROM Y=Y1 TO
!       Y=Y2 AND Z=Z1 TO Z=Z2. THE SOLUTE MAY BE SUBJECT TO FIRST-ORDER
!       CHEMICAL TRANSFORMATION. THE SOLUTION CONTAINS AN INTEGRAL
!       FROM 0 TO T**.25 WHICH IS EVALUATED NUMERICALLY USING A GAUSS-
!       LEGENDRE QUADRATURE TECHNIQUE.
!
      PI=3.14159265358979D0
      CN=0.0D0
!
!       FOR T=0, ALL CONCENTRATIONS EQUAL 0.0
      IF(T.LE.0.0D0) RETURN
!
!       FOR X=0.0, CONCENTRATIONS ARE SPECIFIED BY BOUNDARY CONDITIONS
      IF(X.GT.0.0D0) GO TO 10
      IF(Y.EQ.Y1.OR.Y.EQ.Y2) THEN
        IF(Z.GT.Z1.AND.Z.LT.Z2) CN=0.50D0
        IF(Z.EQ.Z1.OR.Z.EQ.Z2) CN=0.25D0
      END IF
      IF(Z.EQ.Z1.OR.Z.EQ.Z2) THEN
        IF(Y.GT.Y1.AND.Y.LT.Y2) CN=0.50D0
      END IF
      IF(Y.GT.Y1.AND.Y.LT.Y2.AND.Z.GT.Z1.AND.Z.LT.Z2) CN=1.0D0
      RETURN
!
!       START NUMERICAL INTEGRATION LOOP
10    SUM=0.0D0
      DO 20 I=1,NMAX
!
!       SCALE THE GAUSS-LEGENDRE COEFFICIENTS TO ACCOUNT FOR THE
!       NON-NORMALIZED LIMITS OF INTEGRATION
!       LIMITS OF INTEGRATION ARE FROM 0 TO T**0.25
      TT=T**0.250D0
      WI=WN(I)
      ZI=TT*(ZN(I)+1.0D0)/2.0D0
      ZSQ=ZI*ZI
      Z4=ZSQ*ZSQ
!
!       TERM 1
      XVT=X-VX*Z4
      EXP1=-XVT*XVT/(4.0D0*DX*Z4)-DK*Z4
      ERFC1=(Y1-Y)/(2.0D0*ZSQ*DSQRT(DY))
      CALL EXERFC(EXP1,ERFC1,Q1)
!
!       TERM 2
      ERFC2=(Y2-Y)/(2.0D0*ZSQ*DSQRT(DY))
      CALL EXERFC(EXP1,ERFC2,Q2)
!
!       TERM 3
      EXP2=0.0D0
      ERFC1=(Z1-Z)/(2.0D0*ZSQ*DSQRT(DZ))
      CALL EXERFC(EXP2,ERFC1,Q3)
      ERFC2=(Z2-Z)/(2.0D0*ZSQ*DSQRT(DZ))
      CALL EXERFC(EXP2,ERFC2,Q4)
      TERM=(Q1-Q2)*(Q3-Q4)*WI/(ZI*ZSQ)
      SUM=SUM+TERM
20    CONTINUE
      SUM=SUM*TT/2.0D0
      CN=SUM*X/(2.0D0*DSQRT(PI*DX))
      RETURN
    END
    
SUBROUTINE SETUP()
    USE SUN_DATA
    IMPLICIT NONE
    INTEGER I, J, L
    DOUBLE PRECISION PI, D
   
    ! PATCH 15. 26. 10. 15. 
    Y1_PATCH = -5.0D0
    Y2_PATCH = 5.0D0
    Z1_PATCH = -2.5D0
    Z2_PATCH = 2.5
    
    ! LAMBDAS
    LAMBDA(1) = 0.05D0
    LAMBDA(2) = 0.02D0
    LAMBDA(3) = 0.01D0
    LAMBDA(4) = 0.005D0
    
    ! VELOCITY
    VEL = 0.2D0
    
    ! DISPERSION
    DISPX = 1.5D0 * VEL
    DISPY = 0.15D0 * VEL
    DISPZ = 0.15D0 * VEL
    
    ! INITIAL CONCENTRATIONS
    C0(1) = 1.0D0
    C0(2) = 0.0D0
    C0(3) = 0.0D0
    C0(4) = 0.0D0
    
    ! A0
    DO I = 1,4
        A0(I) = C0(I)
        DO J = 1, I -1
            PI = 1.0D0
            DO L = J, I - 1
                D = LAMBDA(L) / (LAMBDA(L) - LAMBDA(I))
                PI = PI * D
            ENDDO
            A0(I) = A0(I) + PI * C0(J) 
        ENDDO
    ENDDO
    
    ! GAUSS-LEGENDRE QUADRATURE POINTS
    NQUAD = 104
    CALL GLQPTS (NQUAD)
    
    ! TIME
    TIME = 400.0D0
    
    RETURN   
END SUBROUTINE SETUP
SUBROUTINE CALC_FROM_FILE(IN_FILE, OUT_FILE)
      USE SUN_DATA
      IMPLICIT NONE
      CHARACTER(len=*), intent(in) :: IN_FILE
      CHARACTER(len=*), intent(in) :: OUT_FILE
      DOUBLE PRECISION :: AI_NORMALIZED, D, PI, X, Y, Z
      INTEGER I, J, L, NODE

      OPEN (20,FILE=OUT_FILE)
      WRITE(20,'(A)') '         X         Y         Z         A         B         C         D'
      OPEN (21,FILE=IN_FILE,STATUS="OLD",ACTION="READ")
      READ(21,*)
      DO
          READ(21,*, END=1) NODE, X, Y, Z
          DO I = 1, 4
	  ! CALCULATE A
	       CALL CNRMLP(LAMBDA(I), TIME, X, Y, Z, &
	       Y1_PATCH, Y2_PATCH, Z1_PATCH, Z2_PATCH, DISPX, DISPY, DISPZ, VEL, AI_NORMALIZED, NQUAD)
	       A(I) = AI_NORMALIZED * A0(I)
          ENDDO
          ! CONVERT A TO C
          DO I = 1,4
	      C(I) = A(I)
	      DO J = 1, I -1
	          PI = 1.0D0
	          DO L = J, I - 1
		      D = LAMBDA(L) / (LAMBDA(L) - LAMBDA(I))
		      PI = PI * D 
	          ENDDO
	          C(I) = C(I) - PI * C(J)
	      ENDDO
          ENDDO   
          ! WRITE X, Y, Z, A, B, C, D  
          WRITE(20,'(7F10.5)') X, Y, Z, (C(I), I = 1,4)
      END DO
    1 CLOSE(21)
      CLOSE(20)
END SUBROUTINE CALC_FROM_FILE