      PROGRAM PATCHI
C.....From Wexler
C
C      ********************************************************
C      *                                                      *
C      *                   **** PATCHI ****                   *
C      *                                                      *
C      *   THREE-DIMENSIONAL GROUND-WATER SOLUTE TRANSPORT    *
C      *                                                      *
C      *    MODEL FOR A SEMI-INFINITE AQUIFER OF INFINITE     *
C      *                                                      *
C      *    WIDTH AND HEIGHT. PATCH SOURCE EXTENDING FROM     *
C      *                                                      *
C      *         Y1 TO Y2 AND Z1 TO Z2 LOCATED AT X=0         *
C      *                                                      *
C      *        GROUND-WATER FLOW IN X-DIRECTION ONLY         *
C      *                                                      *
C      *            VERSION CURRENT AS OF 04/01/90            *
C      *                                                      *
C      ********************************************************
C
C       THE FOLLOWING CARD MUST BE CHANGED IF PROBLEM DIMENSIONS ARE
C       GREATER THAN THOSE GIVEN HERE.
C         MAXX = MAXIMUM NUMBER OF X-VALUES
C         MAXY = MAXIMUM NUMBER OF Y-VALUES
C         MAXZ = MAXIMUM NUMBER OF Z-VALUES
C         MAXT = MAXIMUM NUMBER OF TIME VALUES
C         MAXXY = MAXX * MAXY
C         MAXXY2 = 2 * MAXX * MAXY
      PARAMETER (MAXX=100,MAXY=50,MAXZ=30,MAXT=20,MAXXY=5000,
     X     MAXXY2=10000)
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      CHARACTER*10 CUNITS,VUNITS,DUNITS,KUNITS,LUNITS,TUNITS
      REAL XP,YP,ZP,CP,TP,DELTA,XPC,YPC,XSCLP,YSCLP
      DIMENSION CXY(MAXX,MAXY),X(MAXX),Y(MAXY),Z(MAXZ),T(MAXT)
      COMMON /PDAT/ XP(MAXX),YP(MAXY),CP(MAXXY),XPC(50),YPC(50),
     1 IFLAG(MAXXY2)
      COMMON /IOUNIT/ IN,IO
       CHARACTER*80 TITLE
       COMMON/CSCH/TITLE
C
C     PROGRAM VARIABLES
C
C           NOTE:  ANY CONSISTANT SET OF UNITS MAY BE USED IN THE
C           MODEL.  NO FORMAT STATEMENTS NEED TO BE CHANGED AS
C           LABELS FOR ALL VARIABLES ARE SPECIFIED IN MODEL INPUT.
C
C     C0       SOLUTE CONCENTRATION AT THE INFLOW BOUNDARY [M/L**3]
C     DX       LONGITUDINAL DISPERSION COEFFICIENT [L**2/T]
C     DY       TRANSVERSE (Y-DIRECTION) DISPERSION COEFFICIENT [L**2/T]
C     DZ       TRANSVERSE (Z-DIRECTION) DISPERSION COEFFICIENT [L**2/T]
C     VX       GROUND-WATER VELOCITY IN X-DIRECTION [L/T]
C     DK       FIRST-ORDER SOLUTE DECAY CONSTANT [1/T]
C     X        X-POSITION AT WHICH CONCENTRATION IS EVALUATED [L]
C     Y        Y-POSITION AT WHICH CONCENTRATION IS EVALUATED [L]
C     Z        Z-POSITION AT WHICH CONCENTRATION IS EVALUATED [L]
C     T        TIME AT WHICH CONCENTRATION IS EVALUATED [T]
C     CN       NORMALIZED CONCENTRATION C/C0 [DIMENSIONLESS]
C     CXY      SOLUTE CONCENTRATION C(X,Y,Z,T) [M/L**3]
C     WS       WIDTH OF PATCH SOLUTE SOURCE [L]
C     HS       HEIGHT OF PATCH SOLUTE SOURCE [L]
C     Y1       Y-COORDINATE OF LOWER LIMIT OF PATCH SOLUTE SOURCE [L]
C     Y2       Y-COORDINATE OF UPPER LIMIT OF PATCH SOLUTE SOURCE [L]
C     Z1       Z-COORDINATE OF LOWER LIMIT OF PATCH SOLUTE SOURCE [L]
C     Z2       Z-COORDINATE OF UPPER LIMIT OF PATCH SOLUTE SOURCE [L]
C
C     NX       NUMBER OF X-POSITIONS AT WHICH SOLUTION IS EVALUATED
C     NY       NUMBER OF Y-POSITIONS AT WHICH SOLUTION IS EVALUATED
C     NZ       NUMBER OF Z-POSITIONS AT WHICH SOLUTION IS EVALUATED
C     NT       NUMBER OF TIME VALUES AT WHICH SOLUTION IS EVALUATED
C     NMAX     NUMBER OF TERMS USED IN GAUSS-LEGENDRE NUMERICAL
C              INTEGRATION TECHNIQUE (MUST EQUAL 4, 20, 60, 104 OR 256)
C
C     IPLT     PLOT CONTROL. IF IPLT>0, CONTOUR MAPS ARE PLOTTED
C     XSCLP    SCALING FACTOR TO CONVERT X TO PLOTTER INCHES
C     YSCLP    SCALING FACTOR TO CONVERT Y TO PLOTTER INCHES
C     DELTA    CONTOUR INCREMENT FOR PLOT. (VALUE BETWEEN 0 AND 1.0)
C
C       CHARACTER VARIABLES USED TO SPECIFY UNITS FOR MODEL PARAMETERS
C     CUNITS   UNITS OF CONCENTRATION (M/L**3)
C     VUNITS   UNITS OF GROUND-WATER VELOCITY (L/T)
C     DUNITS   UNITS OF DISPERSION COEFFICIENT (L**2/T)
C     KUNITS   UNITS OF SOLUTE DECAY CONSTANT  (1/T)
C     LUNITS   UNITS OF LENGTH (L)
C     TUNITS   UNITS OF TIME (T)
C
C       DEFINE INPUT/OUTPUT FILES AND PRINT TITLE PAGE
      CALL OFILE
      CALL WTITLE
      WRITE(IO,201)
C
C       READ IN MODEL PARAMETERS
      READ(IN,*) NX,NY,NZ,NT,NMAX,IPLT
      WRITE(IO,205) NX,NY,NZ,NT,NMAX
      nxy=nx*ny
      nxyz=nxy*nz
      READ(IN,*) CUNITS,VUNITS,DUNITS,KUNITS,LUNITS,TUNITS
      READ(IN,*) C0,VX,DX,DY,DZ,DK
      WRITE(IO,210) C0,CUNITS,VX,VUNITS,DX,DUNITS,DY,DUNITS,DZ,DUNITS,
     1 DK,KUNITS
      READ(IN,*) Y1,Y2,Z1,Z2
      WRITE(IO,212) Y1,LUNITS,Y2,LUNITS,Z1,LUNITS,Z2,LUNITS
      READ(IN,*) (X(I),I=1,NX)
      WRITE(IO,215) LUNITS
      WRITE(IO,220) (X(I),I=1,NX)
      READ(IN,*) (Y(I),I=1,NY)
      WRITE(IO,216) LUNITS
      WRITE(IO,220) (Y(I),I=1,NY)
      READ(IN,*) (Z(I),I=1,NZ)
      WRITE(IO,217) LUNITS
      WRITE(IO,220) (Z(I),I=1,NZ)
      READ(IN,*) (T(I),I=1,NT)
      WRITE(IO,225) TUNITS
      WRITE(IO,220) (T(I),I=1,NT)
c..      IF(IPLT.GT.0) READ(IN,*) XSCLP,YSCLP,DELTA
c..      IF(IPLT.GT.0) WRITE(IO,227) XSCLP,YSCLP,DELTA,CUNITS
C.....Write static data to file 13 for visualization
       WRITE(13,5001) TITLE
 5001 FORMAT(A)
      WRITE(13,5001) '    f    t    f    f'
      WRITE(13,5003) NX,NY,nz,NXY,nxyz
 5003 FORMAT(5I5)
      WRITE(13,5002) (X(I),I=1,NX)
 5002 FORMAT(10(1PG12.5))
      WRITE(13,5002) (Y(J),J=1,NY)
      WRITE(13,5002) (Z(K),K=1,NZ)
C
C       READ IN GAUSS-LEGENDRE POINTS AND WEIGHTING FACTORS
      CALL GLQPTS (NMAX)
C
C       Begin time loop
      DO 20 IT=1,NT
C
C       Begin z loop
      DO 30 IZ=1,NZ
C
C       Begin x loop
      DO 40 IX=1,NX
C
C     CALCULATE NORMALIZED CONCENTRATION FOR ALL Y AT X=X(IX) AND Z=Z(IZ)
      DO 50 IY=1,NY
      CALL CNRMLP(DK,T(IT),X(IX),Y(IY),Z(IZ),Y1,Y2,Z1,Z2,DX,
     1 DY,DZ,VX,CN,NMAX)
      CXY(IX,IY)=C0*CN
50    CONTINUE
40    CONTINUE
C
C       PRINT OUT TABLES OF CONCENTRATION VALUES
      NPAGE=1+(NY-1)/9
      DO 60 NP=1,NPAGE
      IF(NP.EQ.1) WRITE(IO,230) T(IT),TUNITS,Z(IZ),LUNITS,LUNITS
      IF(NP.NE.1) WRITE(IO,231) T(IT),TUNITS,Z(IZ),LUNITS,LUNITS
      NP1=(NP-1)*9
      NP2=9
      IF((NP1+NP2).GT.NY) NP2=NY-NP1
      WRITE(IO,235) (Y(NP1+J),J=1,NP2)
      WRITE(IO,236) CUNITS,LUNITS
      DO 70 IX=1,NX
      WRITE(IO,240) X(IX),(CXY(IX,NP1+J),J=1,NP2)
      IF(MOD(IX,45).NE.0) GO TO 70
      WRITE(IO,231) T(IT),TUNITS,Z(IZ),LUNITS,LUNITS
      WRITE(IO,235) (Y(NP1+J),J=1,NP2)
      WRITE(IO,236) CUNITS,LUNITS
70    IF(MOD(IX,5).EQ.0 .AND. MOD(IX,45).NE.0) WRITE(IO,241)
60    CONTINUE
C
C       CONVERT X AND Y TO SINGLE PRECISION AND DIVIDE BY THE
C       PLOT SCALING FACTORS. CONVERT C(X,Y) AND DIVIDE BY C0 TO PLOT
C       CONTOUR MAPS OF NORMALIZED CONCENTRATION FOR EACH TIME VALUE.
      IF(IPLT.gt.0) then
      NXY=NX*NY
      DO 80 I=1,NX
      IP=(I-1)*NY
      XP(I)=SNGL(X(I))
      DO 80 J=1,NY
      IF(I.EQ.1) YP(J)=SNGL(Y(J))
      CP(IP+J)=SNGL(CXY(I,J)/C0)
80    CONTINUE
      TP=SNGL(T(IT))
      ZP=SNGL(Z(IZ))
      NXY2=NXY*2
c..      CALL PLOT3D (XP,YP,ZP,CP,TP,DELTA,NX,NY,NXY,NXY2,IZ,NZ,IPLT,
c..     1 TUNITS,LUNITS,XSCLP,YSCLP,XPC,YPC,IFLAG)
       endif
30    CONTINUE
C.....Write to file 13 for visualization
      WRITE(13,5015) 'Time =',TP
 5015 FORMAT(TR5,A,1PG15.4)
            WRITE(13,5012) 'Concentration'
 5012       FORMAT(A80)
            DO 25 J=1,NY
   25             WRITE(13,5013) (CP(NY*(I-1)+J),I=1,NX)
 5013       FORMAT(9(1PG14.6))
20    CONTINUE
      CLOSE (IN)
      CLOSE (IO)
      close(13)
      STOP 'Calculations completed'
C
C        FORMAT STATEMENTS
101   FORMAT(20I4)
105   FORMAT(8A10)
110   FORMAT(8F10.0)
  201 FORMAT(/////1H ,'Analytical solution to the three-dimensional'
     1     /1H ,'advective-dispersive solute transport equation'/
     2     1H ,'for a semi-infinite aquifer of infinite widthextent'/
     3     1H ,'and height with a continuous patch source at X=0.'
     4     ///1H0,'INPUT DATA'/1H ,10(1H-))
205   FORMAT(1H0,25X,'NUMBER OF X-COORDINATES (NX) = ',I4/1H ,25X,
     1 'NUMBER OF Y-COORDINATES (NY) = ',I4/1H ,25X,
     2 'NUMBER OF Z-COORDINATES (NZ) = ',I4/1H ,25X,
     3 'NUMBER OF TIME VALUES (NT) = ',I4/1H ,25X,
     4 'NUMBER OF POINTS FOR NUMERICAL INTEGRATION (NMAX) = ',I4)
210   FORMAT(1H0,'Solute Concentration on Region Boundary (C0) =',
     1 1P1E13.6,1X,A10/1H ,
     2 'Ground-water Velocity in X-direction (VX) =',1P1E13.6,1X,A10/
     3 1H ,'Dispersion in the X-direction (DX) =',1P1E13.6,1X,A10/
     4 1H ,'Dispersion in the Y-direction (DY) =',1P1E13.6,1X,A10/
     5 1H ,'Dispersion in the Z-direction (DZ) =',1P1E13.6,1X,A10/
     6 1H ,'First-order Solute Decay Rate (DK) =',1P1E13.6,1X,A10)
212   FORMAT(1H0,'AQUIFER WIDTH (W) AND HEIGHT (H) ARE INFINITE'/
     2 1H ,'SOLUTE SOURCE IS LOCATED BETWEEN Y1 =',1P1E13.6,1X,A10/
     3 1H ,tr30,'and Y2 =',1P1E13.6,1X,A10/1H ,tr30,
     4 'Z1 =',1P1E13.6,1X,A10/1H ,tr25,
     5 'and Z2 =',1P1E13.6,1X,A10)
215   FORMAT(1H0,'X-COORDINATES AT WHICH SOLUTE CONCENTRATIONS ',
     1 'WILL BE CALCULATED, IN ',A10/1H ,78(1H-)/)
216   FORMAT(1H0,'Y-COORDINATES AT WHICH SOLUTE CONCENTRATIONS ',
     1 'WILL BE CALCULATED, IN ',A10/1H ,78(1H-)/)
217   FORMAT(1H0,'Z-COORDINATES AT WHICH SOLUTE CONCENTRATIONS ',
     1 'WILL BE CALCULATED, IN ',A10/1H ,78(1H-)/)
220   FORMAT(1H ,5X,8F12.4)
225   FORMAT(1H0,'TIMES AT WHICH SOLUTE CONCENTRATIONS '
     1 'WILL BE CALCULATED, IN ',A10/1H ,70(1H-)/)
227   FORMAT(1H0,'PLOT SCALING FACTOR FOR X (XSCLP) =',1P1E13.6/
     1 1H ,'PLOT SCALING FACTOR FOR Y (YSCLP) =',1P1E13.6/
     2 1H ,'CONTOUR INCREMENT (DELTA) =',1P1E13.6,1X,A10)
230   FORMAT(//15X,'Solute Concentration at time =',
     1 F12.4,1X,A10/35X,'and at Z =',F12.4,1X,A10/
     2 tr15,'Y-coordinate, in ',A10)
231   FORMAT(//15X,'Solute Concentration at time =',
     1 F12.4,1X,A10,5X,'(continued)'/35X,'and at Z =',F12.4,1X,A10/
     2 tr15,'Y-coordinate, in ',A10)
235   FORMAT(1H ,tr10,9F12.4)
236   FORMAT(1H ,tr9,'*',108(1H-)/
     1 1H ,'X-COORDINATE,',2X,'!',44X,'SOLUTE CONCENTRATION, IN '
     2 A10/1H ,'IN ',A10,2X,1H!/1H ,tr15,'!')
240   FORMAT(1H ,F12.3,' !',9F11.5)
241   FORMAT(1H ,tr14,' !')
      END
      SUBROUTINE CNRMLP(DK,T,X,Y,Z,Y1,Y2,Z1,Z2,DX,DY,DZ,VX,CN,NMAX)
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      COMMON /IOUNIT/ IN,IO
      COMMON /GLPTS/ WN(256),ZN(256)
C
C       THIS ROUTINE CALCULATES THE NORMALIZED CONCENTRATION AT X,Y,Z
C       BASED ON THE ANALYTIC SOLUTION TO THE THREE-DIMENSIONAL
C       ADVECTIVE-DISPERSIVE SOLUTE TRANSPORT EQUATION FOR A SEMI-
C       INFINITE AQUIFER WITH INFINITE WIDTH AND HEIGHT. THE SOLUTE
C       SOURCE HAS A FINITE WIDTH AND HEIGHT, EXTENDING FROM Y=Y1 TO
C       Y=Y2 AND Z=Z1 TO Z=Z2. THE SOLUTE MAY BE SUBJECT TO FIRST-ORDER
C       CHEMICAL TRANSFORMATION. THE SOLUTION CONTAINS AN INTEGRAL
C       FROM 0 TO T**.25 WHICH IS EVALUATED NUMERICALLY USING A GAUSS-
C       LEGENDRE QUADRATURE TECHNIQUE.
C
      PI=3.14159265358979D0
      CN=0.0D0
C
C       FOR T=0, ALL CONCENTRATIONS EQUAL 0.0
      IF(T.LE.0.0D0) RETURN
C
C       FOR X=0.0, CONCENTRATIONS ARE SPECIFIED BY BOUNDARY CONDITIONS
      IF(X.GT.0.0D0) GO TO 10
      IF(Y.EQ.Y1.OR.Y.EQ.Y2) THEN
        IF(Z.GT.Z1.AND.Z.LT.Z2) CN=0.50D0
        IF(Z.EQ.Z1.OR.Z.EQ.Z2) CN=0.25D0
      END IF
      IF(Z.EQ.Z1.OR.Z.EQ.Z2) THEN
        IF(Y.GT.Y1.AND.Y.LT.Y2) CN=0.50D0
      END IF
      IF(Y.GT.Y1.AND.Y.LT.Y2.AND.Z.GT.Z1.AND.Z.LT.Z2) CN=1.0D0
      RETURN
C
C       START NUMERICAL INTEGRATION LOOP
10    SUM=0.0D0
      DO 20 I=1,NMAX
C
C       SCALE THE GAUSS-LEGENDRE COEFFICIENTS TO ACCOUNT FOR THE
C       NON-NORMALIZED LIMITS OF INTEGRATION
C       LIMITS OF INTEGRATION ARE FROM 0 TO T**0.25
      TT=T**0.250D0
      WI=WN(I)
      ZI=TT*(ZN(I)+1.0D0)/2.0D0
      ZSQ=ZI*ZI
      Z4=ZSQ*ZSQ
C
C       TERM 1
      XVT=X-VX*Z4
      EXP1=-XVT*XVT/(4.0D0*DX*Z4)-DK*Z4
      ERFC1=(Y1-Y)/(2.0D0*ZSQ*DSQRT(DY))
      CALL EXERFC(EXP1,ERFC1,Q1)
C
C       TERM 2
      ERFC2=(Y2-Y)/(2.0D0*ZSQ*DSQRT(DY))
      CALL EXERFC(EXP1,ERFC2,Q2)
C
C       TERM 3
      EXP2=0.0D0
      ERFC1=(Z1-Z)/(2.0D0*ZSQ*DSQRT(DZ))
      CALL EXERFC(EXP2,ERFC1,Q3)
      ERFC2=(Z2-Z)/(2.0D0*ZSQ*DSQRT(DZ))
      CALL EXERFC(EXP2,ERFC2,Q4)
      TERM=(Q1-Q2)*(Q3-Q4)*WI/(ZI*ZSQ)
      SUM=SUM+TERM
20    CONTINUE
      SUM=SUM*TT/2.0D0
      CN=SUM*X/(2.0D0*DSQRT(PI*DX))
      RETURN
      END
